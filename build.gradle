plugins {
	id "org.springframework.boot" version '3.4.4'
    id "io.spring.dependency-management" version '1.1.7'
	id 'java'
	id 'jacoco'
	id "io.freefair.lombok" version "8.14.2"
}

group = 'de.unimarburg.diz'
version = '0.0.2-SNAPSHOT'
java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/snapshot' }
	maven { url 'https://repo.spring.io/milestone' }
}

sourceSets {
	intTest {
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output
	}
}

configurations {
	intTestImplementation.extendsFrom implementation
	intTestRuntimeOnly.extendsFrom runtimeOnly
}

tasks.register('integrationTest', Test) {
	description = 'Runs integration tests.'
	group = 'verification'

	testClassesDirs = sourceSets.intTest.output.classesDirs
	classpath = sourceSets.intTest.runtimeClasspath
	shouldRunAfter test
}

check.dependsOn integrationTest

ext {
	set('springCloudVersion', "2023.0.5")
	set('testContainersVersion', '1.20.6')
}

dependencies {
	// spring cloud stream kafka
	implementation 'org.springframework.kafka:spring-kafka'

	implementation 'org.apache.httpcomponents:httpclient:4.5.14'

	//implementation 'org.springframework.cloud:spring-cloud-stream-binder-kafka-streams'
	//implementation ('org.springframework.cloud:spring-cloud-stream')

	// spring boot
	implementation "org.springframework.boot:spring-boot-starter-web"
	implementation "org.springframework.boot:spring-boot-starter-actuator"
	//implementation 'org.yaml:snakeyaml:2.2'

	// metrics
	implementation 'io.micrometer:micrometer-registry-prometheus'
	implementation 'io.micrometer:micrometer-core'

	// mapper
	// https://mvnrepository.com/artifact/com.opencsv/opencsv
	implementation 'com.opencsv:opencsv:5.9'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

	implementation 'com.microsoft.sqlserver:mssql-jdbc:12.10.0.jre11'

	// unit tests
	testImplementation 'org.springframework.cloud:spring-cloud-stream'
	testImplementation 'org.apache.kafka:kafka-streams-test-utils:3.9.0'

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.kafka:spring-kafka-test'
	testImplementation 'org.assertj:assertj-core:3.27.7'

	// integration tests
	intTestImplementation 'org.springframework.kafka:spring-kafka-test'
	intTestImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	intTestImplementation "org.testcontainers:kafka:$testContainersVersion"
	intTestImplementation "org.testcontainers:junit-jupiter:$testContainersVersion"
	intTestImplementation 'org.awaitility:awaitility:4.2.1'
	intTestImplementation 'org.assertj:assertj-core:3.27.7'


}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

test {
	useJUnitPlatform()
}

integrationTest {
	useJUnitPlatform()
}

jacoco {
	toolVersion = "0.8.12"
	reportsDirectory = layout.buildDirectory.dir('jacoco-reports')
}

jacocoTestReport {
	reports {
		xml.required = true
		csv.required = true
		csv.outputLocation = layout.buildDirectory.file('jacoco/coverage.csv')
		html.outputLocation = layout.buildDirectory.dir('jacoco/html')
	}
}

jar {
	enabled = false
}

bootJar {
	mainClass.set('de.unimarburg.diz.mtbpidtokafka.Application')
}
